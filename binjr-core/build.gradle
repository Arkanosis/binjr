/*
 *    Copyright 2017-2018 Frederic Thevenet
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */


plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dependencies {
    api 'org.apache.logging.log4j:log4j-core:2.11.1'
    api 'org.apache.httpcomponents:httpclient:4.5.6'
    api 'org.controlsfx:controlsfx:9.0.0'
    api 'org.gillius:jfxutils:1.0'
    api 'com.google.code.gson:gson:2.8.5'
    api 'org.apache.commons:commons-csv:1.5'
    api 'org.glassfish.jaxb:jaxb-runtime:2.4.0-b180830.0438'
    api 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls,javafx.fxml,javafx.swing'
        ]
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'eu.fthevenet.binjr.Bootstrap',
                'Specification-Title': project.name,
                'Specification-Version': project.version,
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'SplashScreen-Image': 'images / splashscreen.png',
                'Build-Number': BINJR_BUILD_NUMBER
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'binjr-core'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "binjr-core"
                description = 'Flexible Time Series Visualization'
                url = 'https://www.binjr.eu'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id =  'fthevenet'
                        name = 'Frederic Thevenet'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/fthevenet/binjr.git'
                    developerConnection = 'scm:git:https://github.com/fthevenet/binjr.git'
                    url = 'https://github.com/fthevenet/binjr'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"

            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            //authentication(userName: System.getenv('OSSRH_JIRA_USERNAME'), password: System.getenv('OSSRH_JIRA_PASSWORD'))
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}




